apply plugin: 'java'

repositories {
  mavenCentral()
}

dependencies {
  compile "org.eclipse.tycho:org.eclipse.jdt.compiler.apt:1.1.0.v20140509-1235"
  compile "org.eclipse.jdt:core:3.3.0-v_771"
  compile files(getToolsJar())
}

def getToolsJar()
{
  def jar = files("${System.properties['java.home']}/../lib/tools.jar",
        "${System.properties['java.home']}/lib/tools.jar",
        "${System.properties['java.home']}/../Classes/classes.jar").find { it.exists() }

  if (jar != null)
  {
    return jar
  }

  throw new GradleException("jdk tools jar not found")
}

cleanEclipse { 
  doLast { 
    delete ".apt_generated"
    delete ".settings"
    delete ".factorypath"
    delete "bin"
  }
}

// no need for source or javadoc jars to be published for build-plugin
nexus {
  attachSources = false
  attachJavadoc = false
}

// don't publish the build plugin
configurations.archives.artifacts.removeAll { return it.name == "build-plugin"; }